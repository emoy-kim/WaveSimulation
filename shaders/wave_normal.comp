#version 430

layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

struct Attributes
{
   float x, y, z, nx, ny, nz, s, t;
};

layout(binding = 0, std430) buffer InOutPoints { Attributes Pn[]; };

uniform ivec2 WavePointNumSize;

void main() 
{
   int x = int(gl_GlobalInvocationID.x);
   int y = int(gl_GlobalInvocationID.y);
   if (x >= WavePointNumSize.x || y >= WavePointNumSize.y) return;

   int index = y * WavePointNumSize.x + x;
   vec3 estimated_normal = vec3(0.0f);
   vec3 point_vec = vec3(Pn[index].x, Pn[index].y, Pn[index].z);

   if (y > 0) {
      int top_index = index - WavePointNumSize.x;
      vec3 top_vec = vec3(Pn[top_index].x, Pn[top_index].y, Pn[top_index].z) - point_vec;
      if (x > 0) {
         vec3 left_vec = vec3(Pn[index - 1].x, Pn[index - 1].y, Pn[index - 1].z) - point_vec;
         vec3 top_left_vec = vec3(Pn[top_index - 1].x, Pn[top_index - 1].y, Pn[top_index - 1].z) - point_vec;
         estimated_normal += cross( top_vec, top_left_vec );
         estimated_normal += cross( top_left_vec, left_vec );
      }
      if (x < WavePointNumSize.x - 1) {
         vec3 right_vec = vec3(Pn[index + 1].x, Pn[index + 1].y, Pn[index + 1].z) - point_vec;
         vec3 top_right_vec = vec3(Pn[top_index + 1].x, Pn[top_index + 1].y, Pn[top_index + 1].z) - point_vec;
         estimated_normal += cross( right_vec, top_right_vec );
         estimated_normal += cross( top_right_vec, top_vec );
      }
   }

   if (y < WavePointNumSize.y - 1) {
      int bottom_index = index + WavePointNumSize.x;
      vec3 bottom_vec = vec3(Pn[bottom_index].x, Pn[bottom_index].y, Pn[bottom_index].z) - point_vec;
      if (x > 0) {
         vec3 left_vec = vec3(Pn[index - 1].x, Pn[index - 1].y, Pn[index - 1].z) - point_vec;
         vec3 bottom_left_vec = vec3(Pn[bottom_index - 1].x, Pn[bottom_index - 1].y, Pn[bottom_index - 1].z) - point_vec;
         estimated_normal += cross( left_vec, bottom_left_vec );
         estimated_normal += cross( bottom_left_vec, bottom_vec );
      }
      if (x < WavePointNumSize.x - 1) {
         vec3 right_vec = vec3(Pn[index + 1].x, Pn[index + 1].y, Pn[index + 1].z) - point_vec;
         vec3 bottom_right_vec = vec3(Pn[bottom_index + 1].x, Pn[bottom_index + 1].y, Pn[bottom_index + 1].z) - point_vec;
         estimated_normal += cross( bottom_vec, bottom_right_vec );
         estimated_normal += cross( bottom_right_vec, right_vec );
      }
   }

   estimated_normal = normalize( estimated_normal );
   Pn[index].nx = estimated_normal.x;
   Pn[index].ny = estimated_normal.y;
   Pn[index].nz = estimated_normal.z;
}